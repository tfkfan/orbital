plugins {
    id 'org.jreleaser' version '1.17.0'
    id 'java'
}

allprojects {
    description = "A blueprint adapting vert.x to a distributed game server"
    group = "io.github.tfkfan"
    version = "1.2.0"
}

subprojects {
    apply plugin: "java"
    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "org.jreleaser"

    java {
        withJavadocJar()
        withSourcesJar()
    }

    repositories {
        mavenCentral()
        mavenLocal()
    }

    var staging = layout.buildDirectory.dir("staging-deploy")

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                pom {
                    name = project.name
                    description = (project.name + ' description')
                    url = 'https://github.com/tfkfan/orbital'

                    developers {
                        developer {
                            id = 'tfkfan'
                            name = 'Artem Baltser'
                            email = 'abaltserdev@outlook.com'
                            organizationUrl = 'https://tfkfan.github.io'
                        }
                    }

                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://github.com/tfkfan/orbital/blob/master/LICENSE.md'
                        }
                    }

                    scm {
                        connection = 'scm:git://github.com/tfkfan/orbital.git'
                        developerConnection = 'scm:ssh://git@github.com:tfkfan/orbital.git'
                        url = 'https://tfkfan.github.io'
                    }
                }
                groupId project.group
                artifactId project.name
                version project.version

                withBuildIdentifier()
            }
        }
        repositories {
            maven {
                setUrl(staging)
            }
        }
    }


    jreleaser {
        gitRootSearch = true
        project {
            authors = ['tfkfan']
            license = 'MIT'
            links {
                homepage = 'https://tfkfan.github.io'
            }
            description = "Orbital"
            inceptionYear = '2025'
        }

        signing {
            active = 'ALWAYS'
            armored = true
            verify = true
        }

        release {
            github {
                skipRelease = true
                skipTag = true
            }
        }

        deploy {
            maven {
                mavenCentral {
                    app {
                        active = 'ALWAYS'
                        url = 'https://central.sonatype.com/api/v1/publisher'
                        sign = true
                        retryDelay = 200
                        checksums = true
                        sourceJar = true
                        javadocJar = true
                        stagingRepository(staging.get().toString())
                    }
                }
            }
        }
    }
}

jreleaserConfig.enabled = false
jreleaserFullRelease.enabled = false
